import pandas as pd
from snakemake.utils import validate, min_version

min_version("5.1.2")

configfile: "config/config.yaml"
workdir: config["work_directory"]


validate(config, schema="schemas/config.schema.yaml")

samples = pd.read_csv(config["samples"], sep="\t").set_index("sample", drop=False)
validate(samples, "schemas/samples.schema.yaml")


replicates = pd.read_csv(config["replicates"], dtype=str, sep="\t").set_index(
    ["sample", "replicate"], drop=False
)
replicates.index = replicates.index.set_levels(
    [i.astype(str) for i in replicates.index.levels]
)  # enforce str in index
validate(replicates, schema="schemas/replicates.schema.yaml")

is_paired = config["paired"]

include: "rules/common.smk"

rule all:
    input:
        "qc/multiqc.html",
        "counts/merged.tsv",
        "counts/merged_raw.tsv",
        "counts/merged_tpm.tsv",
        expand(
            [
                "deseq2/{contrast}.diffexp.tsv",
                "deseq2/{contrast}.ma-plot.svg",
            ],
            contrast=config["diffexp"]["contrasts"],
        ),
        # The first rule should define the default target files
        # Subsequent target rules can be specified below. They should start with all_*.

rule skip_de:
    input:
        "qc/multiqc.html",
        "counts/merged.tsv",
        "counts/merged_raw.tsv",
        "counts/merged_tpm.tsv"
# container: "docker://continuumio/miniconda3"


include: "rules/qc.smk"
include: "rules/trim.smk"
include: "rules/genome_index.smk"
include: "rules/align.smk"
include: "rules/counts.smk"
include: "rules/diff_expression.smk"
